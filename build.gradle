import org.apache.tools.ant.filters.ReplaceTokens
import org.ajoberstar.gradle.git.release.opinion.Strategies

plugins {
	id 'org.springframework.boot' version '2.4.0'
	id 'io.spring.dependency-management' version '1.0.10.RELEASE'
	id 'java'
	id 'com.palantir.git-version' version '0.7.3'

	//another way of versioning plugin
	id 'org.ajoberstar.grgit' version '1.7.2'
	id 'org.ajoberstar.release-opinion' version '1.7.2'
}

apply plugin: "com.palantir.git-version"

group = 'by.jrr.versioning'
version = gitVersion()
sourceCompatibility = '11'

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	compile("org.springframework.boot:spring-boot-starter-web")
	compile("org.springframework.boot:spring-boot-starter-actuator")
	// https://mvnrepository.com/artifact/javax.validation/validation-api
	compile group: 'javax.validation', name: 'validation-api', version: '2.0.1.Final'
}



test {
	useJUnitPlatform()
}

task showVersion {
	doLast {
		println "\nCurrent version: ${gitVersion()}\n"

		def details = versionDetails()
		println "last tag          : ${details.lastTag}"
		println "commit distance   : ${details.commitDistance}"
		println "hash              : ${details.gitHash}"
		println "branch name       : ${details.branchName}"
		println "is clean tag      : ${details.isCleanTag}"
	}
}

processResources {
	filter(ReplaceTokens, tokens:[appVersion: gitVersion()])
}

// versioning with org.ajoberstar.release-opinion
release {
	versionStrategy Strategies.FINAL
	defaultVersionStrategy Strategies.SNAPSHOT
	tagStrategy {
		generateMessage = { version -> "Version $project.version" }
	}
}
